buildscript {
    ext {
        kotlin_version = '1.2.0'
        ktor_version = '0.9.0'
        exposed_version = '0.9.1'
        mysql_version = '6.0.6'
        logback_version = '1.2.1'
        kluent_version = '1.31'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
}

group 'kissybnts'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = 1.8

mainClassName = 'io.ktor.server.netty.DevelopmentEngine'

repositories {
    mavenCentral()
    jcenter()
    maven { url  "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/kotlin/exposed" }
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-locations:$ktor_version"
    compile "io.ktor:ktor-auth:$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version"
    compile("org.jetbrains.exposed:exposed:$exposed_version") {
        exclude(module: "log4j")
        exclude(module: "slf4j-log4j12")
        exclude(module: "kotlin-stdlib")
    }
    compile "mysql:mysql-connector-java:$mysql_version"

    compile ('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.2')
    compile "io.jsonwebtoken:jjwt:0.9.0"
    compile "ch.qos.logback:logback-classic:$logback_version"

    // Testing
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.2'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile("org.amshove.kluent:kluent:$kluent_version")
    testRuntime('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

flyway {
    def host = System.getenv('DB_HOST')?: '127.0.0.1'
    def port = System.getenv('DB_PORT')?: '3306'
    def name = System.getenv('DB_NAME')?: 'ktor-todo'
    user = System.getenv('DB_USER')?: 'user'
    password = System.getenv('DB_PASS')?: 'user'
    url = "jdbc:mysql://$host:$port/$name"
}